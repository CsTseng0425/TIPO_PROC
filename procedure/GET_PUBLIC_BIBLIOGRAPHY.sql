--------------------------------------------------------
--  DDL for Procedure GET_PUBLIC_BIBLIOGRAPHY
--------------------------------------------------------
set define off;

  CREATE OR REPLACE PROCEDURE "S193"."GET_PUBLIC_BIBLIOGRAPHY" (
  P_APPL_NO IN CHAR, 
  APPL_NO OUT CHAR,           --申請案號
  APPL_DATE OUT CHAR,         --申請日期
  PATENT_NAME_C OUT VARCHAR2, --專利中文名稱
  PATENT_NAME_E OUT VARCHAR2, --專利英文名稱
  NOTICE_NO_2 OUT CHAR,       --公開號
  NOTICE_DATE_2 OUT CHAR,     --公開日
  APPL_FLAG OUT CHAR,         --實體審查申請註記
  IPCS OUT SYS_REFCURSOR,         --國際專利分類
  APPLICANTS OUT SYS_REFCURSOR,   --申請人資料
  INVENTORS OUT SYS_REFCURSOR,    --發明人資料
  PROXYS OUT SYS_REFCURSOR,        --代理人資料
  PRIORITY_RIGHTS OUT SYS_REFCURSOR --優先權資料
)IS 
  V_SPT82_NOTICE_NO_2   SPT82.NOTICE_NO_2%TYPE;
  V_SPM21B_STEP_CODE    SPM21B.STEP_CODE%TYPE;
  V_SPM21B_IPC_REF_TYPE SPM21B.IPC_REF_TYPE%TYPE;
BEGIN
  SELECT A.APPL_NO, A.APPL_DATE, A.PATENT_NAME_C, A.PATENT_NAME_E, 
         B.NOTICE_NO_2, B.NOTICE_DATE_2,
         C.APPL_FLAG
    INTO APPL_NO, APPL_DATE, PATENT_NAME_C, PATENT_NAME_E,
         NOTICE_NO_2, NOTICE_DATE_2,
         APPL_FLAG
    FROM SPT31 A, SPT82 B, (
           SELECT APPL_NO, MAX(APPL_FLAG) AS APPL_FLAG
             FROM SPT31F 
            WHERE APPL_NO = P_APPL_NO
            GROUP BY APPL_NO
         ) C
   WHERE A.APPL_NO = B.APPL_NO(+)
     AND A.APPL_NO = C.APPL_NO(+)
     AND A.APPL_NO = P_APPL_NO;
  
  BEGIN
    SELECT NOTICE_NO_2
      INTO V_SPT82_NOTICE_NO_2
      FROM SPT82
     WHERE APPL_NO = P_APPL_NO;
    IF TRIM(V_SPT82_NOTICE_NO_2) IS NOT NULL THEN
      V_SPM21B_STEP_CODE := 'B';
      V_SPM21B_IPC_REF_TYPE := '7';
    ELSE
      V_SPM21B_STEP_CODE := 'V';
      V_SPM21B_IPC_REF_TYPE := '6';
    END IF;
  EXCEPTION
    WHEN no_data_found THEN
      V_SPM21B_STEP_CODE := 'B';
      V_SPM21B_IPC_REF_TYPE := '6';
  END;
  
  OPEN IPCS FOR
    SELECT IPC_CODE_MS, IPC_CODE_DT, SUBSTR(VERSION_NO, 1, 4) || '.' || SUBSTR(VERSION_NO, 5, 2) AS VERSION_NO
      FROM SPM21B
     WHERE APPL_NO = P_APPL_NO
       AND STEP_CODE = V_SPM21B_STEP_CODE
       AND IPC_REF_TYPE = V_SPM21B_IPC_REF_TYPE;
    
  OPEN APPLICANTS FOR
    SELECT NAME_C, NAME_E, ADDR_C, ADDR_E 
      FROM SPM11 
     WHERE APPL_NO = P_APPL_NO
       AND ID_TYPE = '1' 
     ORDER BY DATA_SEQ;
  
  OPEN INVENTORS FOR 
    SELECT NAME_C, NAME_E 
      FROM SPM11 
     WHERE APPL_NO = P_APPL_NO
       AND ID_TYPE = '2' 
     ORDER BY DATA_SEQ;
  
  OPEN PROXYS FOR
    SELECT A.NAME_C 
      FROM SPM61 A, SPM11A B 
     WHERE A.ATTORNEY_NO = B.ATTORNEY_NO 
       AND B.APPL_NO = P_APPL_NO;
  
  OPEN PRIORITY_RIGHTS FOR
    SELECT PRIORITY_APPL_NO, 
           PRIORITY_DATE,
           CASE 
             WHEN PRIORITY_NATION_ID IS NULL THEN 
               PRIORITY_NATION_ID 
             ELSE
               SPMF5.NATIONAL_NAME_C 
           END AS NATION_NAME
      FROM SPT32 
      LEFT JOIN SPMF5 
        ON SPT32.PRIORITY_NATION_ID = SPMF5.NATIONAL_ID
     WHERE PRIORITY_FLAG = '1' 
       AND APPL_NO = P_APPL_NO
     ORDER BY DATA_SEQ;
  
END GET_PUBLIC_BIBLIOGRAPHY;

/
